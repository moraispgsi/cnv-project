plugins {
    id 'org.hidetake.ssh' version '2.9.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'application'


    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile "junit:junit:4+"
        compile group: 'com.amazonaws', name: 'aws-java-sdk', version: '1.11.313'
    }

    applicationDefaultJvmArgs = ['-XX:-UseSplitVerifier']



    ssh.settings {
        knownHosts = allowAnyHosts //fix me maybe..
    }

    task deploy {
        dependsOn( ':'+ project.name +':distZip')
        doLast {
            ssh.run {
                session(remotes.webServer) {
                    put from: 'build/distributions/' + project.name + '.zip', into: '/home/ec2-user/'
                    execute 'unzip -o /home/ec2-user/' + project.name + '.zip && rm /home/ec2-user/' + project.name + '.zip'
                }
            }
        }
    }

}

project(':AutoScaler') {
    mainClassName = 'pt.ulisboa.tecnico.meic.cnv.autoscaler.AutoScaler'

    //Deploy to AWS
    remotes {
        webServer {
            host = 'ec2-35-180-33-92.eu-west-3.compute.amazonaws.com'
            user = 'ec2-user'
            identity = file("../keypairs/loadbalancerAndAutoScalerKeypair.pem")
        }
    }
}

project(':LoadBalancer') {
    mainClassName = 'pt.ulisboa.tecnico.meic.cnv.loadbalancer.WebServer'

    //Deploy to AWS
    remotes {
        webServer {
            host = 'ec2-35-180-33-92.eu-west-3.compute.amazonaws.com'
            user = 'ec2-user'
            identity = file("../keypairs/loadbalancerAndAutoScalerKeypair.pem")
        }
    }
}


project(':server') {
    mainClassName = 'pt.ulisboa.tecnico.meic.cnv.WebServer'

    dependencies {
        compile project(':MazeRunner')
        compile project(':inst')
    }


    //Deploy to AWS
    remotes {
        webServer {
            host = 'ec2-35-180-33-92.eu-west-3.compute.amazonaws.com'
            user = 'ec2-user'
            identity = file("../keypairs/ec2InstanceKeyPair.pem")
        }
    }

    distZip {
        into('server') {
            from '..'
            include 'resources/*/*'
        }
    }

}

project(':MazeRunner') {
    mainClassName = 'pt.ulisboa.tecnico.meic.cnv.MazeRunner'

    jar.dependsOn(':inst:run')
}

project('inst') {
    mainClassName = 'pt.ulisboa.tecnico.meic.cnv.instrument.Instrumentation'


    //Instrumenting MazeRunner classes
    run {
        dependsOn(':MazeRunner:compileJava')
        args = ['../MazeRunner/build/classes/java/main/pt/ulisboa/tecnico/meic/cnv/mazerunner/maze']
    }



}

